plugins {
  id "base"
  id "org.asciidoctor.convert" version "1.5.3"
  id "org.ajoberstar.git-publish" version "0.3.0"
}

group "daggerok"
version "0.0.1"

asciidoctorj {
  version = "1.5.5"
}

asciidoctor {
  sourceDir = project.file("src")
  sources {
    include "index.adoc"
  }
  outputDir = project.file("$buildDir/asciidoc")
  attributes toc: "left",
      idprefix: "",
      docinfo1: "",
      icons: "font",
      idseparator: "-",
      setanchors: "true",
      "source-highlighter": "coderay",
      "toc-title": "Table of Contents",
      "build-gradle": file("build.gradle"),
      "endpoint-url": "https://daggerok.github.io/ratpack-examples"
}

// read more: https://github.com/ajoberstar/gradle-git-publish
gitPublish {
  repoUri = "git@github.com:daggerok/ratpack-examples.git"
  branch = "gh-pages"
  contents {
    from "$buildDir/asciidoc/html5"
    from "$projectDir/src/static"
    into "."
  }
}

task docs {
  doLast {
    file("$buildDir/gitPublish")
      .renameTo(file("$projectDir.parent/docs"))
  }
  dependsOn asciidoctor, ":asciidoc:gitPublishCopy"
  shouldRunAfter clean, asciidoctor, ":asciidoc:gitPublishCopy", build
}

clean.doFirst {
  delete "$projectDir.parent/docs"
}

asciidoctor.shouldRunAfter clean
build.dependsOn asciidoctor, gitPublishCopy
gitPublishCopy.shouldRunAfter clean, asciidoctor

task wrapper(type: Wrapper) {
  gradleVersion = "4.3.1"
  distributionType = "ALL"
}
